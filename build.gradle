/*
 * Copyright 2016 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */


buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3"
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'io.codearte.nexus-staging'

group = 'cd.go.plugin'
archivesBaseName = 'gocd-package-material-plugin-shim'
version = '16.12.0'

sourceCompatibility = 1.7
targetCompatibility = 1.7

sourceSets {
    main {
        java {
            srcDirs = ['src/']
        }
    }
    test {
        java {
            srcDirs = ['test/']
        }
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

repositories {
    mavenCentral()
    maven { url "http://repo.maven.apache.org/maven2" }
}

dependencies {
    compile group: 'cd.go.plugin', name: 'go-plugin-api', version:'16.11.0'
    compile group: 'com.google.code.gson', name: 'gson', version:'2.3.1'
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'Shim for GoCD package repository poller plugin'
        packaging 'jar'
        description 'A shim to help upgrade GoCD package repository poller plugins to work with GoCD 17.1 (if they use the old API-based extension point)'
        url 'https://www.go.cd/'

        scm {
          connection 'scm:git:https://github.com/gocd-contrib/gocd-package-material-plugin-shim/'
          developerConnection 'scm:git:https://github.com/gocd-contrib/gocd-package-material-plugin-shim/'
          url 'https://github.com/gocd-contrib/gocd-package-material-plugin-shim/'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'gocd-team'
            name 'GoCD Team'
            email 'go-cd@googlegroups.com'
          }
        }
      }
    }
  }
}

nexusStaging {
    packageGroup = "cd.go"
    delayBetweenRetriesInMillis = 5000
}